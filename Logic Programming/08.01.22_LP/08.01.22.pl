:- use_module(library(clpfd)).

% парче(X, Y) - При параметри X и Y предикатът "парче" генерира в Y всички парчета на нивката X.
парче([], []).
парче([(A,_)|Z], [A|Y]) :-
    парче(Z, Y).
парче([_|Z], Y) :-
    парче(Z, Y).


% втори_елементи_на_парче(X, Y, Z) - При параметри X, Y и Z предикатът "втори_елементи_на_парче" генерира в Z списък
% от вторите елементи на тези двойки от X(нивка), чиито първи елементи са включени в Y(парче).
втори_елементи_на_парче(_, [], []).
втори_елементи_на_парче([(A,B)|X], [A|Y], [B|Z]) :-
    втори_елементи_на_парче(X, Y, Z).
втори_елементи_на_парче([(A,_)|X], [C|Y], Z) :-
    A #\= C,
    втори_елементи_на_парче(X, [C|Y], Z).


% сума(X, N) - При параметри X и N предикатът "сума" е истина тогава и само тогава, когато N е сумата на
% елементите на списъка X.
% Следното условие е достатъчно, за да няма зацикляне с предиката: Списъкът X е с известна дължина.
сума([], N) :- N #= 0.
сума([A|X], N) :- сума(X, N-A).


% размер_парче(X, Y, N) - При параметри X, Y и N предикатът "размер_парче" е истина тогава и само тогава, когато N е
% размерът на списъка Y(парче), получен от списъка X(нивка).
% Следното условие е достатъчно, за да няма зацикляне с предиката: Списъкът X е с известна дължина.
размер_парче(X, Y, N) :-
    втори_елементи_на_парче(X, Y, Z),
    сума(Z, N).


% елемент(A, X) - При параметри A и X предикатът "елемент" е истина тогава и само тогава, когато A е елемент на X.
% Следното условие е достатъчно, за да няма зацикляне с предиката: Списъкът X е с известна дължина.
елемент(A, [A|_]).
елемент(A, [_|Y]) :- елемент(A,Y).


% сечение(X, Y, Z) - При параметри X, Y и Z предикатът "сечение" е истина тогава и само тогава, когато
% списъкът Z е сечението на списъците X и Y.
% Следното условие е достатъчно, за да няма зацикляне с предиката: Списъкът X е с известна дължина.
сечение([], _, []).
сечение(_, [], []).
сечение([A|X], Y, [A|Z]) :-
    елемент(A, Y),
    сечение(X, Y, Z).
сечение([A|X], Y, Z) :-
    not(елемент(A, Y)),
    сечение(X, Y, Z).


indepen(L) :-
    R #>= 1,
    K #= M*N,
    парче(L, A),
    парче(L, B),
    label(A),
    label(B),
    сечение(A, B, X),
    length(X, R),
    размер_парче(L, A, N),
    размер_парче(L, B, M),
    размер_парче(L, X, K).
